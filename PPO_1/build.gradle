// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        getVersionName = {
            def gitDescribe = ['git', 'describe'].execute().text.trim()
            if (!gitDescribe.isEmpty()) {
                def name = (gitDescribe =~ /(\d+)\.(\d+)(-(\d+)-)?/)[-1]
                return "${name[1]}.${name[2]}.${name[4] ?: 0}"
            } else {
                return "0.0.${getCommitCount()}"
            }
        }

        getCommitCount = {
            return Integer.parseInt(['git', 'rev-list', '--count', 'HEAD'].execute().text.trim())
        }

        kotlin_version = '1.3.41'
        version_name = getVersionName()
        version_code = getCommitCount()
    }

    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
